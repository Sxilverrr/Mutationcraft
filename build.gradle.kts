plugins {
    id("dev.architectury.loom")
    id("architectury-plugin")
}

val minecraft = stonecutter.current.version

version = "${mod.version}-$minecraft"
base {
    archivesName.set("${mod.id}-common")
}

architectury.common(stonecutter.tree.branches.mapNotNull {
    if (stonecutter.current.project !in it) null
    else it.project.prop("loom.platform")
})

repositories {
    maven("https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/")
}

dependencies {
    minecraft("com.mojang:minecraft:$minecraft")
    mappings(loom.officialMojangMappings())
    modImplementation("net.fabricmc:fabric-loader:${mod.dep("fabric_loader")}")
    if (stonecutter.eval(minecraft, ">=1.21"))
        compileOnly("software.bernie.geckolib:geckolib-common-$minecraft:${mod.dep("geckolib")}") { isTransitive = false }
    else
        modCompileOnly("software.bernie.geckolib:geckolib-fabric-$minecraft:${mod.dep("geckolib")}") { isTransitive = false }
}

java {
    withSourcesJar()
    val java = if (stonecutter.eval(minecraft, ">=1.20.5"))
        JavaVersion.VERSION_21 else JavaVersion.VERSION_17
    targetCompatibility = java
    sourceCompatibility = java
}

tasks.withType<JavaCompile> {
    options.compilerArgs.addAll(listOf("-Xmaxerrs", "100000"))
}

tasks.build {
    group = "versioned"
    description = "Must run through 'chiseledBuild'"
}
